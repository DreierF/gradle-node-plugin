import org.gradle.util.GradleVersion

plugins {
    id "com.github.node-gradle.node"
}

node {
    pnpmVersion = "4.12.4"
    download = true
    workDir = file("build/node")
}

// mocha is installed locally whereas eslint is not

task lint(type: PnpxTask) {
    dependsOn pnpmInstall
    command = "eslint@6.3.0"
    args = ["src", "test"]
    inputs.file(".eslintrc.yml")
    inputs.dir("src")
    inputs.dir("test")
    outputs.upToDateWhen {
        true
    }
}

task test(type: PnpxTask) {
    dependsOn lint
    command = "mocha"
    inputs.file("package.json")
    inputs.dir("src")
    inputs.dir("test")
    outputs.upToDateWhen {
        true
    }
}

task env(type: PnpxTask) {
    command = "print-env"
    outputs.upToDateWhen {
        true
    }
}

task cwd(type: PnpxTask) {
    command = "cwd"
    outputs.upToDateWhen {
        true
    }
}

task version(type: PnpxTask) {
    dependsOn pnpmInstall
    command = "--version"
}

if (isPropertyEnabled("changeInputs")) {
    lint.args = ["src"]
    test.command = "_mocha"
}

def isPropertyEnabled(String name) {
    if (GradleVersion.current() >= GradleVersion.version("6.6")) {
        return providers.systemProperty(name).forUseAtConfigurationTime().isPresent()
    }
    return System.properties[name] != null
}
